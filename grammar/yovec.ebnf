%import common.WS
%ignore WS

NUMBER: /-?\d+(\.\d{1,4})?/
VAR_IDENT: /[A-Z]+/
YOLOL_IDENT: /[a-zA-Z][a-zA-Z0-9]*|:[a-zA-Z0-9]+/

// Statements

program: line*
line: import | export | let

import: "import" YOLOL_IDENT
export: "export" VAR_IDENT

?let: "let" VAR_IDENT "=" vector     -> letvec
    | "let" VAR_IDENT "=" range      -> letran
    | "let" VAR_IDENT "=" repeat     -> letrep
    | "let" VAR_IDENT "=" vexpr      -> letvexpr

vector: "[" (nexpr ",")* nexpr ("," nexpr)* "]"
range: "range" "(" (nexpr ",")? nexpr (nexpr ",")? ")"
repeat: "repeat" "(" vector nexpr ")"

// Numbers

?num_unary_op: "abs"        -> abs
             | "sqrt"       -> sqrt
             | "sin"        -> sin
             | "cos"        -> cos
             | "tan"        -> tan
             | "arcsin"     -> arcsin
             | "arccos"     -> arccos
             | "arctan"     -> arctan

?num_binary_op: "+"     -> add
              | "-"     -> sub
              | "*"     -> mul
              | "/"     -> div
              | "%"     -> mod
              | "^"     -> exp

?nexpr: num_unary_op nexpr              -> unary
      | nexpr num_binary_op nexpr       -> binary
      | "reduce" num_binary_op vexpr    -> reduce
      | vexpr "dot" vexpr               -> dot
      | vexpr "cross" vexpr             -> cross
      | "len" vexpr                     -> len
      | "$" YOLOL_IDENT                 -> external
      | NUMBER                          -> number
      | "(" nexpr ")"

// Vectors

?vec_unary_op: "-"  -> vneg

?vec_binary_op: "+"     -> vadd
              | "-"     -> vsub

?logical_op: "<"    -> lt
           | "<="   -> le
           | ">"    -> gt
           | ">="   -> ge
           | "=="   -> eq
           | "!="   -> ne

?vexpr: "map" num_binary_op nexpr vexpr     -> premap
      | "map" nexpr num_binary_op vexpr     -> postmap
      | "filter" logical_op nexpr vexpr     -> filter
      | "concat" vexpr vexpr                -> concat
      | vec_unary_op vexpr                  -> vecunary
      | vexpr vec_binary_op vexpr           -> vecbinary
      | VAR_IDENT                           -> variable
      | "(" vexpr ")"
